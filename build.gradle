allprojects {
    apply plugin: 'groovy'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
    }

    group 'sandbox.example.drools'
    version '1.0-SNAPSHOT'

    dependencies {
        testCompile "junit:junit:$junitVersion"
        compile "org.codehaus.groovy:groovy-all:$groovyVersion"
        compile "org.springframework.boot:spring-boot-starter:$springBootVersion"
        compile "org.drools:drools-decisiontables:$droolsVersion"
        compile "org.kie:kie-api:$droolsVersion"
        compile("org.kie:kie-spring:$droolsVersion") {
            // Sadly kie-spring depends upon spring 3.2, but we need 4x to run smoothly with spring boot
            exclude group: "org.springframework"
        }

    }
}

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

apply plugin: 'io.spring.dependency-management'

project(":tests-holder") {

    apply plugin: 'org.springframework.boot'

    mainClassName = 'sandbox.example.drools.Main'

    bootRun {
        if ( project.hasProperty('jvmArgs') ) {
            jvmArgs = (project.jvmArgs.split("\\s+") as List)

        }
    }
}